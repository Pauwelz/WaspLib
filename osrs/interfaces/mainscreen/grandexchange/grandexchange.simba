

{$DEFINE WL_GRANDEXCHANGE_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type

  EGEOfferProgress = enum(NONE, PROGRESSING, COMPLETED, ABORTED);


  EGESlotType = enum(EMPTY, SELL, BUY);


  TRSGrandExchangeSlot = record
    Typ: EGESlotType;
    Bounds, Header, BuyButton, SellButton: TBox;
    ItemBox, ItemNameBox, ValueBox, StatusBox: TBox;
  end;

procedure TRSGrandExchangeSlot._Setup(bounds: TBox);
begin
  Self.Bounds := bounds;
  with bounds do
  begin
    Self.Header.X1 := X1+3;
    Self.Header.Y1 := Y1+4;
    Self.Header.X2 := X2-3;
    Self.Header.Y2 := Y1+22;

    Self.BuyButton.X1 := X1+6;
    Self.BuyButton.Y1 := Y1+43;
    Self.BuyButton.X2 := Self.BuyButton.X1+46;
    Self.BuyButton.Y2 := Self.BuyButton.Y1+45;

    Self.SellButton := Self.BuyButton.Offset([56,0]);

    Self.ItemBox.X1 := X1+6;
    Self.ItemBox.Y1 := Y1+35;
    Self.ItemBox.X2 := Self.ItemBox.X1+35;
    Self.ItemBox.Y2 := Self.ItemBox.Y1+31;

    Self.ItemNameBox.X1 := X1+44;
    Self.ItemNameBox.Y1 := Y1+33;
    Self.ItemNameBox.X2 := X2-3;
    Self.ItemNameBox.Y2 := Self.ItemNameBox.Y1+36;

    Self.StatusBox.X1 := X1+4;
    Self.StatusBox.Y1 := Y2-35;
    Self.StatusBox.X2 := X2-4;
    Self.StatusBox.Y2 := Y2-21;

    Self.ValueBox.X1 := X1+4;
    Self.ValueBox.Y1 := Y2-20;
    Self.ValueBox.X2 := X2-4;
    Self.ValueBox.Y2 := Y2-4;
  end;
end;


function TRSGrandExchangeSlot.GetType(): EGESlotType;
var
  count: Integer;
begin
  //no need for ocr, color count is lighter and should work fine.
  count := Target.CountColor(RSColors.TEXT_ORANGE, 0, Self.Header);
  case count of
    154: Result := EGESlotType.EMPTY;
    90:  Result := EGESlotType.SELL;
    101: Result := EGESlotType.BUY;
  end;

  Self.Typ := Result;
end;



function TRSGrandExchangeSlot.Contains(item: TRSItem): Boolean;
var
  match: TImageMatch;
begin
  Result := ItemFinder.Find([item], [Self.ItemBox], match);
end;


function TRSGrandExchangeSlot.Discover(): TRSItemArray;
var
  img: TImage;
  hash: String;
begin
  img := Target.GetImage(Self.ItemBox);
  hash := ItemFinder.GetHash(img);
  if hash = '' then Exit;
  Result := ItemFinder.GetHashItems(hash);
end;


function TRSGrandExchangeSlot.ReadItem(): TRSItem;
begin
  Result := OCR.RecognizeLines(Self.ItemNameBox, RSFonts.PLAIN_11, [RSColors.TEXT_LIGHT_ORANGE], 0).Join(' ');
end;


function TRSGrandExchangeSlot.Value(): Integer;
var
  str: String;
begin
  str := OCR.Recognize(Self.ValueBox, RSFonts.PLAIN_11, [RSColors.TEXT_ORANGE], 0);
  Result := str.ExtractInteger(-1);
end;


function TRSGrandExchangeSlot.GetProgress(): EGEOfferProgress;
begin
  if Target.HasColor($005F00, 0, 1, Self.StatusBox) then
    Exit(EGEOfferProgress.COMPLETED);
  if Target.HasColor($2080D8, 0, 1, Self.StatusBox) then
    Exit(EGEOfferProgress.PROGRESSING);
  if Target.HasColor($00008F, 0, 1, Self.StatusBox) then
    Exit(EGEOfferProgress.ABORTED);

  Result := EGEOfferProgress.NONE;
end;



type

  TRSGrandExchange = record
    Title: TRSInterfaceTitle;
    Bounds: TBox;
    Slots: array [0..7] of TRSGrandExchangeSlot;
    HistoryButton, CollectButton: TBox;
  end;

function TRSGrandExchange.IsOpen(): Boolean; forward;


procedure TRSGrandExchange.SetupInterface();
var
  i: Integer;
  tba: TBoxArray;
begin
  case RSClient.Mode of
    ERSMode.FIXED: Self.Bounds := MSInterface.CreateBounds([0, 2, 0, -1], 484, 304);
    ERSMode.RESIZABLE, ERSMode.MODERN_COMPACT, ERSMode.MODERN_WIDE:
      Self.Bounds := MSInterface.CreateBounds([0, 1, 0, -2], 484, 304);
  end;

  Self.Title.Setup(Self.Bounds);
  Self.Title.IsOpen := @Self.IsOpen;

  tba := TBoxArray.Create(Self.Bounds.TopLeft + [9,65], 4, 2, 114, 110, [3, 9]);
  for i := 0 to High(tba) do
    Self.Slots[i]._Setup(tba[i]);

  with Self.Bounds do
    Self.HistoryButton := [X1+7, Y1+7, X1+53, Y1+26];
end;


function TRSGrandExchange.IsOpen(): Boolean;
begin
  if BankPin.IsOpen() then
  begin
    if not BankPin.WaitLoading(3000) then
      raise GetDebugLn('BankPin', 'GrandExchange pin buttons don''t seem to have loaded in 3 seconds.');

    if not BankPin.Enter(Profiles.GetPin()) then
      raise GetDebugLn('BankPin', 'Failed to enter GrandExchange pin.');
  end;

  Result := Self.Title.GetTitle() = 'Grand Exchange';
end;


function TRSGrandExchange.WaitOpen(time: Integer; interval: Integer = -1): Boolean;
begin
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(Self.IsOpen(), interval, time);
end;


function TRSGrandExchange.Close(escape: Boolean): Boolean;
begin
  Result := Self.Title.Close(escape);
end;

function TRSGrandExchange.Close(escapeProbability: Single = -1): Boolean; overload;
begin
  Result := Self.Title.Close(escapeProbability);
end;

var

  GrandExchange: TRSGrandExchange;
