

{$DEFINE WL_LAMP_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type

  TRSLamp = record
    Title: TRSInterfaceTitle;
    Bounds, ConfirmBounds: TBox;
    Slots: TRSButtonArray;
  end;

function TRSLamp.IsOpen(): Boolean; forward;


procedure TRSLamp.SetupInterface();
const
  SKILL_ORDER: array [0..22] of ERSSkill = [
    ERSSkill.ATTACK, ERSSkill.STRENGTH, ERSSkill.RANGED, ERSSkill.MAGIC,
    ERSSkill.DEFENCE, ERSSkill.HITPOINTS, ERSSkill.PRAYER, ERSSkill.AGILITY,
    ERSSkill.HERBLORE, ERSSkill.THIEVING, ERSSkill.CRAFTING,
    ERSSkill.RUNECRAFTING, ERSSkill.SLAYER, ERSSkill.FARMING, ERSSkill.MINING,
    ERSSkill.SMITHING, ERSSkill.FISHING, ERSSkill.COOKING, ERSSkill.FIREMAKING,
    ERSSkill.WOODCUTTING, ERSSkill.FLETCHING, ERSSkill.CONSTRUCTION, ERSSkill.HUNTER
  ];
var
  i: Integer;
  tba: TBoxArray;
begin
  Self.Bounds := MSInterface.CreateBounds([0,0,0,0], 300, 260);

  Self.Title.Bounds.X1 := Self.Bounds.X1+8;
  Self.Title.Bounds.Y1 := Self.Bounds.Y1+38;
  Self.Title.Bounds.X2 := Self.Bounds.X2-8;
  Self.Title.Bounds.Y2 := Self.Title.Bounds.Y1 + 22;

  Self.Title.CloseButton.X1 := Self.Bounds.X2+69;
  Self.Title.CloseButton.Y1 := Self.Bounds.Y1-20;
  Self.Title.CloseButton.X2 := Self.Bounds.X2+92;
  Self.Title.CloseButton.Y2 := Self.Bounds.Y1+2;

  Self.Title.TitleColor := $999999;
  Self.Title.IsOpen := @Self.IsOpen;

  with Self.Bounds.TopLeft do
    tba := TBoxArray.Create([X+64,Y+78], 5, 1, 26, 26, [10,10]) +
           TBoxArray.Create([X+46,Y+114], 6, 3, 26, 26, [10,10]);

  SetLength(Self.Slots, Length(tba));

  for i := 0 to High(tba) do
  begin
    Self.Slots[SKILL_ORDER[i]].Bounds := tba[i];
    Self.Slots[SKILL_ORDER[i]].EnabledColors := [[$0F1043, 0], [$23269F, 0.227]];
  end;

  Self.ConfirmBounds.X1 := Self.Bounds.X1 + 81;
  Self.ConfirmBounds.Y1 := Self.Bounds.Y2 - 35;
  Self.ConfirmBounds.X2 := Self.Bounds.X2 - 81;
  Self.ConfirmBounds.Y2 := Self.Bounds.Y2 - 13;
end;


function TRSLamp.IsOpen(): Boolean;
begin
  Result := Self.Title._IsOpen() and Target.HasColor($999999, 0, 819, Self.Title.Bounds);
end;


function TRSLamp.WaitOpen(time: Integer; interval: Integer = -1): Boolean;
begin
  Result := Self.Title.WaitOpen(time, interval);
end;



function TRSLamp.Open(item: TRSItem = 'Lamp'): Boolean;
begin
  if Self.IsOpen() then
    Exit(True);

  if Inventory.Items.Interact(item, 'Rub') then
    Result := Self.WaitOpen(1200);
end;



function TRSLamp.Close(escape: Boolean): Boolean;
begin
  Result := Self.Title.Close(escape);
end;

function TRSLamp.Close(escapeProbability: Single = -1): Boolean; overload;
begin
  Result := Self.Title.Close(escapeProbability);
end;



function TRSLamp.GetSelected(): ERSSkill;
begin
  for Result := Low(ERSSkill) to High(ERSSkill)-1 do
    if Self.Slots[Result].Enabled() then
      Exit;
  Result := ERSSkill.TOTAL;
end;


function TRSLamp.Enable(skill: ERSSkill): Boolean;
begin
  Result := Self.Slots[skill].Enable();
end;


function TRSLamp.Confirm(): Boolean;
begin
  if not Self.IsOpen() then
    Exit;
  Mouse.Click(Self.ConfirmBounds, EMouseButton.LEFT);
  Result := SleepUntil(not Self.IsOpen(), 50, 600);
end;


function TRSLamp.Select(skill: ERSSkill): Boolean;
begin
  if not Self.Open() then
    Exit;
  Result := Self.Slots[skill].Enable() and Self.Confirm();
end;

var

  Lamp: TRSLamp;
