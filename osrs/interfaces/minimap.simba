

{$DEFINE WL_MINIMAP_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type

  ERSMinimapDot  = enum(PLAYER, NPC, ITEM);
  ERSMinimapDots = set of ERSMinimapDot;
  TRSMinimapDotArray = array [ERSMinimapDot] of TPointArray;


  ERSMinimapOrb = enum(HITPOINTS, PRAYER, ENERGY, SPECIAL);

  TRSMinimapOrb = record
    Bounds : TBox;
    Circle : TCircle;
  end;


  TRSMinimap = record
    Orbs: array of TRSMinimapOrb;
    Bounds: TBox;
    Center: TPoint;
    Radius: Integer;
    Polygon: TPolygon;

    Compass: record
      Circle: TCircle;
      Accuracy: Double;
    end;

    Mask: TPointArray;

    const DotIMGs: array [ERSMinimapDot] of TImage = [
      ImageFromString('IMG:AQAAAAIAAAACAAAAAAAAAFzsuAkAAAAACAAAAAAAAAAgAAAAAAAAAMBkSgABAAAAAAAAAAAAAAAgAAAAAAAAAOD7MgcAAAAArlsBAAEAAACQU1wAAAAAAJAcugYAAAAAtnayBgAAAACIHLoGAAAAAFzsuAkAAAAAXOy4CQAAAAAg/DIHAAAAAHBgAQABiVBORw0KGgoAAAANSUhEUgAAAAIAAAACCAIAAAD91JpzAAAABnRSTlMAAAAAAABupgeRAAAAEUlEQVR4nGP4BwYMQPDmzRsARBgIuYq1W6UAAAAASUVORK5CYII='),
      ImageFromString('IMG:AQAAAAIAAAACAAAAAAAAANf7QpQykwAAEDgu0v9/AADABkYIAAAAABgAAAAAAAAAAAAAAAAAAADAAV4AAQAAAND7MgcAAAAAf00BAAEAAAAX+0KUMpMAAIRosgYAAAAAAAAAAAAAAACEaLIGAAAAABD8MgcAAAAAkaIAAAEAAAAQ/DIHAAAAAAAAAAAAiVBORw0KGgoAAAANSUhEUgAAAAIAAAACCAIAAAD91JpzAAAABnRSTlMAAAAAAABupgeRAAAAFElEQVR4nGP484fp61cGEHj7lgEAL5AFvzCPessAAAAASUVORK5CYII='),
      ImageFromString('IMG:AQAAAAIAAAACAAAAAAAAANf7QpQykwAAEDgu0v9/AABgG7AHAAAAABgAAAAAAAAAAAAAAAAAAADAAV4AAQAAAND7MgcAAAAAf00BAAEAAAAX+0KUMpMAAIRosgYAAAAAAAAAAAAAAACEaLIGAAAAABD8MgcAAAAAkaIAAAEAAAAQ/DIHAAAAAAAAAAAAiVBORw0KGgoAAAANSUhEUgAAAAIAAAACCAIAAAD91JpzAAAABnRSTlMAAAAAAABupgeRAAAAFUlEQVR4nGP4p6Dwh42NAQg+MjAAABzlAzh9c63TAAAAAElFTkSuQmCC')
    ];
    const FLAG: TImage = ImageFromString('IMG:AQAAAAIAAAACAAAAAAAAAIDjPwEAAAAA7joBAAEAAACQ4z8BAAAAALEMAQABAAAAgH8GCQAAAAAAiAYJAAAAAAgAAAAIAAAAgH8GCQAAAAABAAAAAgAAAAIAAAAYAAAAAAAAAAAAAAAAAAAAAwAAABgIEAgICAAAAAEAAAIAAAAAAAAAAAAAAAAAAAAAiVBORw0KGgoAAAANSUhEUgAAAAIAAAACCAIAAAD91JpzAAAAFUlEQVR4nGP4z8AQayzIcCyMBUgBABrmA2K2r4OdAAAAAElFTkSuQmCC');
  end;


procedure TRSMinimap.SetupInterface();
var
  img: TImage;
  tpa: TPointArray;
  i: Integer;
begin
  MainScreen.Mask.DrawColor := $FFFFFF;

  case RSClient.Mode of
    ERSMode.RESIZABLE, ERSMode.MODERN_COMPACT, ERSMode.MODERN_WIDE:
    begin
      img := new TImage(WLEnv.AssetsDir + 'images' + PATH_SEP + 'mm_resizable_mask.png');
      Self.Mask := img.FindColor($0, 0).Invert().Grow(3).ExtractBox([0, 0, img.Width-1, img.Height-1]);

      with RSClient.Bounds do
      begin
        Self.Bounds.X1 := X2 - 157;
        Self.Bounds.X2 := X2 - 6;
        Self.Bounds.Y1 := Y1 + 8;
        Self.Bounds.Y2 := Y1 + 159;
      end;

      Self.Radius := 71;
    end;

    ERSMode.FIXED:
    begin
      img := new TImage(WLEnv.AssetsDir + 'images' + PATH_SEP + 'mm_fixed_mask.png');
      Self.Mask := img.FindColor($0, 0).Invert().Grow(3).ExtractBox([0, 0, img.Width-1, img.Height-1]);

      with RSClient.Bounds do
      begin
        Self.Bounds.X1 := X2 - 194;
        Self.Bounds.X2 := X2 - 50;
        Self.Bounds.Y1 := Y1 + 9;
        Self.Bounds.Y2 := Y1 + 159;
      end;

      Self.Radius := 65;
    end;
  end;

  with Self.Bounds do
    case RSClient.Mode of
      ERSMode.RESIZABLE, ERSMode.MODERN_COMPACT, ERSMode.MODERN_WIDE:
      begin
        Self.Compass.Circle := [X1 - 2,  Y1 + 14,  17];
        Self.Orbs[ERSMinimapOrb.HITPOINTS].Circle := [X1 - 13, Y1 + 56,  12];
        Self.Orbs[ERSMinimapOrb.PRAYER].Circle    := [X1 - 14, Y1 + 89,  12];
        Self.Orbs[ERSMinimapOrb.ENERGY].Circle    := [X1 - 3,  Y1 + 122, 12];
        Self.Orbs[ERSMinimapOrb.SPECIAL].Circle   := [X1 + 18, Y1 + 147, 12];
        tpa := [[0, -76], [21, -73], [40, -64], [56, -51], [68, -33], [75, -1], [67, 34], [52, 43], [40, 62], [21, 72], [0, 75], [-21, 72], [-40, 62], [-56, 49], [-68, 31], [-75, -1], [-68, -33], [-56, -51], [-40, -64], [-21, -73]];
      end;

      ERSMode.FIXED:
      begin
        Self.Compass.Circle := [X1 - 9,  Y1 + 11,  16];
        Self.Orbs[ERSMinimapOrb.HITPOINTS].Circle := [X1 - 15, Y1 + 48,  12];
        Self.Orbs[ERSMinimapOrb.PRAYER].Circle    := [X1 - 15, Y1 + 82,  12];
        Self.Orbs[ERSMinimapOrb.ENERGY].Circle    := [X1 - 4,  Y1 + 115, 12];
        Self.Orbs[ERSMinimapOrb.SPECIAL].Circle   := [X1 + 17, Y1 + 140, 12];
        tpa := [[21, -71], [39, -62], [55, -49], [67, -32], [70, 0], [66, 19], [57, 32], [41, 43], [20, 58], [12, 71], [0, 74], [-11, 72], [-24, 55], [-47, 39], [-60, 31], [-66, 22], [-71, 0], [-72, -14], [-67, -32], [-55, -49], [-39, -62], [-21, -71], [0, -74]];
      end;
    end;

  MainScreen.Mask.DrawCircleFilled(Self.Compass.Circle.Expand(4));

  for i := 0 to High(Self.Orbs) do
  begin
    with Self.Orbs[i].Circle do
      Self.Orbs[i].Bounds := TBox.Create(X-35, Y-1, X-15, Y+9);
    MainScreen.Mask.DrawCircleFilled(Self.Orbs[i].Circle.Expand(6));
  end;

  with Self.Orbs[ERSMinimapOrb.HITPOINTS].Bounds do
    MainScreen.Mask.DrawBoxFilled([X1 - 5, Y1 - 6, X2 + 7, Y2 + 5]);
  with Self.Orbs[ERSMinimapOrb.PRAYER].Bounds do
    MainScreen.Mask.DrawBoxFilled([X1 - 4, Y1 - 5, X2 + 7, Y2 + 6]);
  with Self.Orbs[ERSMinimapOrb.ENERGY].Bounds do
    MainScreen.Mask.DrawBoxFilled([X1 - 5, Y1 - 6, X2 + 7, Y2 + 5]);
  with Self.Orbs[ERSMinimapOrb.SPECIAL].Bounds do
    MainScreen.Mask.DrawBoxFilled([X1 - 4, Y1 - 6, X2 + 7, Y2 + 5]);

  Self.Center := Self.Bounds.Center;
  Self.Polygon := tpa.Offset(Self.Center);
  MainScreen.Mask.DrawPolygonFilled(Self.Polygon.Expand(9));
  MainScreen.Mask.DrawCircleFilled(Self.Center.Offset(63,56), 14);
end;


function TRSMinimap.Contains(pt: TPoint): Boolean;
begin
  Result := Self.Polygon.Contains(pt);
end;


function TRSMinimap.Filter(tpa: TPointArray): TPointArray;
begin
  Result := tpa.ExtractPolygon(Self.Polygon);
end;



function TRSMinimap.RandomPoint(pt: TPoint; randomness: Integer): TPoint;
begin
  if not Self.Polygon.Contains(pt) then
    raise GetDebugLn('Minimap', ToStr(pt) + ' is not a valid point to generate a random point.');

  repeat
    Result := pt.Random(-randomness, randomness);
  until Self.Polygon.Contains(Result);
end;





property TRSMinimap.CompassRadians: Single;
var
  center: TPoint;
  bounds: TBox;
  north,south,west,east: TPoint;
  Nclr, dials, southArr, westArr, eastArr: TPointArray;
  n,w,e,s,x,y: Single;
begin
  center := Self.Compass.Circle.Center;
  bounds := Self.Compass.Circle.Bounds;

  dials := Target.FindColor($26259C,  12, bounds);
  Nclr  := Target.FindColor($1D2931, 0.6, bounds);

  dials := dials.ExtractDist(center, 10, 20);
  north := Nclr.ExtractDist(center, 0,20).Mean();

  southArr := dials.ExtractDist(north, 21, 50);
  south    := southArr.FurthestPoint(center);

  dials  := dials.Difference(southArr);

  for dials in dials.Cluster(3) do
  begin
    if CrossProduct(dials.Mean(), north, south) > 0 then
      westArr := dials
    else
      eastArr := dials;
  end;

  west := westArr.FurthestPoint(eastArr.Mean()); //late edit: Should not this be middle, not eastarr.mean
  east := eastArr.FurthestPoint(westArr.Mean()); //late edit: Should not this be middle, not westarr.mean

  n := ArcTan2(north.y-center.y, north.x-center.x) + HALF_PI;
  s := ArcTan2(south.y-center.y, south.x-center.x) - HALF_PI;
  w := ArcTan2(west.y-center.y, west.x-center.x)+PI;
  e := ArcTan2(east.y-center.y, east.x-center.x);

  x := Cos(s) + Cos(e) + Cos(w) + Cos(n);
  y := Sin(s) + Sin(e) + Sin(w) + Sin(n);

  Result := ArcTan2(y/4, x/4);
  if Result < 0 then
    Result := Result + TAU;
end;

property TRSMinimap.CompassRadians(radians: Double): Boolean;
const
  RAD_PER_PIXEL: Double = 162.2004206;
var
  available: TBox;
  remain, oneDeg: Double;
  clockwise: Boolean;
  destination: TPoint;
  pixels: Integer;
  timeout: UInt64;
begin
  available := RSClient.Bounds.Expand(-10);
  radians := RadNormalize(radians);
  timeout := Time() + RandomMean(6000, 8000);
  oneDeg := DegToRad(1);

  repeat
    remain := DeltaAngle(Self.CompassRadians, radians, TAU);
    if Abs(remain) <= Self.Compass.Accuracy then
      Exit(True);

    pixels := Round(Abs(remain) * RAD_PER_PIXEL);
    clockwise := InRange(remain, oneDeg, PI) or (remain < -PI);

    if clockwise then
    begin
      if (not available.Contains(Target.MouseXY)) or ((Target.MouseY + 200 > available.Y2) or (Target.MouseX + pixels > available.X2)) then
        Mouse.Move([available.X1, available.Y1, available.X1 + 200, available.Y1 + 200]);

      destination.X := Target.MouseX + pixels;
    end
    else
    begin
      if (not available.Contains(Target.MouseXY)) or ((Target.MouseY + 200 > available.Y2) or (Target.MouseX - pixels < available.X1)) then
        Mouse.Move([available.X2 - 200, available.Y1, available.X2, available.Y1 + 200]);

      destination.X := Target.MouseX - pixels;
    end;

    destination.Y := Random(Target.MouseY + 100, available.Y2);

    Mouse.Drag(destination, EMouseButton.MIDDLE);
    Biometrics.Sleep(100, 1000, ERandomDir.LEFT);
  until Time() > timeout;

  WriteLn GetDebugLn('Minimap', 'Setting `CompassRadians` timed out.', ELogLevel.WARN);

  if not RSClient.IsLoggedIn() then
    Login.DoLogin()
  else
    WriteLn GetDebugLn('Minimap', 'Make sure the setting "Middle mouse button controls the camera" is enabled in the game settings.', ELogLevel.WARN);
end;

property TRSMinimap.CompassRadians(minRadians, maxRadians: Single): Boolean;
var
  radians: Single;
begin
  radians := RandomMean(RadNormalize(minRadians), RadNormalize(maxRadians));
  Result := Self.CompassRadians := radians;
end;



property TRSMinimap.CompassDegrees: Single;
begin
  Result := RadToDeg(Self.CompassRadians);
end;

property TRSMinimap.CompassDegrees(degrees: Single): Boolean;
begin
  Result := Self.CompassRadians := DegToRad(degrees);
end;

property TRSMinimap.CompassDegrees(minDegrees, maxDegrees: Single): Boolean;
begin
  Result := Self.CompassRadians[DegToRad(minDegrees), DegToRad(maxDegrees)];
end;



function TRSMinimap.GetLevel(orb: ERSMinimapOrb): Integer;
begin
  Result := OCR.RecognizeShadow(Self.Orbs[orb].Bounds, RSFonts.PLAIN_11, 0).ExtractInteger();
end;


function TRSMinimap.UnderLevel(orb: ERSMinimapOrb; level: Integer): Boolean;
begin
  Result := Self.GetLevel(orb) < level;
end;


function TRSMinimap.OverLevel(orb: ERSMinimapOrb; level: Integer): Boolean;
begin
  Result := Self.GetLevel(orb) > level;
end;







function TRSMinimap.HasPoison(): Boolean;
var
  b: TBox;
begin
  with Self.Orbs[ERSMinimapOrb.HITPOINTS].Circle.Bounds do
    b := [X1+3, Y2-4, X2-3, Y2+1];

  Result := Target.HasColor($004007, 8, 1, b);
end;


function TRSMinimap.HasVenom(): Boolean;
var
  b: TBox;
begin
  with Self.Orbs[ERSMinimapOrb.HITPOINTS].Circle.Bounds do
    b := [X1+3, Y2-4, X2-3, Y2+1];
  Result := Target.HasColor([$2D351B, 8.330, EColorSpace.HSV, [0.882, 1.148, 0.971]], 1, b);
end;


function TRSMinimap.HasStamina(): Boolean;
var
  b: TBox;
begin
  with Self.Orbs[ERSMinimapOrb.ENERGY].Circle.Center do
    b := TBox.Create([X,Y], 4, 4);
  Result := Target.HasColor([$2D61C1, 1.475, EColorSpace.HSV, [2.455, 0.300, 0.247]], 1, b);
end;


function TRSMinimap.HasSpecial(): Boolean;
var
  b: TBox;
begin
  with Self.Orbs[ERSMinimapOrb.SPECIAL].Circle.Bounds do
    b := [X1+3, Y2-4, X2-3, Y2];
  Result := Target.HasColor([$6A6330, 12.309, EColorSpace.HSV, [1.620, 0.532, 0.850]], 1, b);
end;





function TRSMinimap.PrayerEnabled(): Boolean;
begin
  Result := Target.HasColor(ColorTolerance($2188AB, 10.980, EColorSpace.HSV, [1.317, 1.076, 0.609]), 8, Self.Orbs[ERSMinimapOrb.PRAYER].Circle.Bounds);
end;


function TRSMinimap.EnergyEnabled(): Boolean;
var
  b: TBox;
begin
  with Self.Orbs[ERSMinimapOrb.ENERGY].Circle.Bounds do
    b := [X1+3, Y2-3, X2-3, Y2];

  Result := Target.HasColor([$2A6C8E, 9.954, EColorSpace.HSV, [1.771, 0.726, 0.505]], 8, b);
end;


function TRSMinimap.SpecialEnabled(): Boolean;
var
  b: TBox;
begin
  with Self.Orbs[ERSMinimapOrb.SPECIAL].Circle.Bounds do
    b := [X1+3, Y2-4, X2-3, Y2];
  Result := Target.HasColor([$718B63, 8.414, EColorSpace.HSV, [0.258, 0.495, 2.249]], 8, b);
end;



function TRSMinimap.GetPercent(orb: ERSMinimapOrb): Integer;
var
  tpa: TPointArray;
  col: TColorTolerance;
begin
  case orb of
    ERSMinimapOrb.HITPOINTS:
    begin
      col := ColorTolerance($030575, 1.242, EColorSpace.RGB, [0.064, 1.068, 1.869]);
      if Self.HasPoison  then col := ColorTolerance($02851C, 1.688, EColorSpace.LCH, [0.201, 0.166, 2.635]);
      if Self.HasVenom then col := ColorTolerance($27361A, 0.983, EColorSpace.LCH, [0.227, 0.346, 2.428]);
    end;
    ERSMinimapOrb.PRAYER:
    begin
      col := ColorTolerance($42232D, 2.354, EColorSpace.HSL, [1.309, 1.314, 0.378]);
      if Self.PrayerEnabled then col := ColorTolerance($643940, 6.384, EColorSpace.HSV, [1.045, 1.532, 0.424]);
    end;
    ERSMinimapOrb.ENERGY:  Exit(Self.GetLevel(orb));
    ERSMinimapOrb.SPECIAL: Exit(Self.GetLevel(orb));
  end;

  tpa := Target.FindColor(col, Self.Orbs[orb].Circle.Bounds.Expand(1));
  if Length(tpa) < 1 then Exit;
  tpa := tpa.SortByY(True);
  Result := Round(((tpa[High(TPA)].Y-tpa[0].Y)/25)*100);
end;



function TRSMinimap.Toggle(orb: ERSMinimapOrb): Boolean;
var
  state: Boolean;
begin
  case orb of
    ERSMinimapOrb.HITPOINTS: Exit;
    ERSMinimapOrb.PRAYER:  state := Self.PrayerEnabled();
    ERSMinimapOrb.ENERGY:  state := Self.EnergyEnabled();
    ERSMinimapOrb.SPECIAL:
      if not Self.HasSpecial() then
        Exit
      else
        state := Self.SpecialEnabled();
  end;

  Mouse.Click(Self.Orbs[orb].Circle, EMouseButton.LEFT);

  case orb of
    ERSMinimapOrb.HITPOINTS: Exit;
    ERSMinimapOrb.PRAYER:  Result := Self.PrayerEnabled() <> state;
    ERSMinimapOrb.ENERGY:  Result := Self.EnergyEnabled() <> state;
    ERSMinimapOrb.SPECIAL: Result := Self.SpecialEnabled() <> state;
  end;
end;



function TRSMinimap.EnablePrayer(): Boolean;
begin
  if Self.PrayerEnabled() then
    Exit(True);

  if Self.GetLevel(ERSMinimapOrb.PRAYER) <= 1 then
    Exit;

  Mouse.Click(Self.Orbs[ERSMinimapOrb.PRAYER].Circle, EMouseButton.LEFT, True);
  Result := SleepUntil(Self.PrayerEnabled(), 200, 2*TICK);
end;


function TRSMinimap.DisablePrayer(): Boolean;
begin
  if not Self.PrayerEnabled() then
    Exit(True);

  if Self.GetLevel(ERSMinimapOrb.PRAYER) <= 1 then
    Exit(True);

  Mouse.Click(Self.Orbs[ERSMinimapOrb.PRAYER].Circle, EMouseButton.LEFT, True);
  Result := SleepUntil(not Self.PrayerEnabled(), 200, 2*TICK);
end;



function TRSMinimap.CurePoison(): Boolean;
begin
  if not Self.HasPoison() then
    Exit(True);

  Mouse.Click(Self.Orbs[ERSMinimapOrb.HITPOINTS].Circle, EMouseButton.LEFT, True);
  Result := SleepUntil(not Self.HasPoison(), 200, 2*TICK);
end;


function TRSMinimap.CureVenom(): Boolean;
begin
  if not Self.HasVenom() then
    Exit(True);

  Mouse.Click(Self.Orbs[ERSMinimapOrb.HITPOINTS].Circle, EMouseButton.LEFT, True);
  Result := SleepUntil(not Self.HasVenom(), 200, 2*TICK);
end;



function TRSMinimap.EnableEnergy(): Boolean;
begin
  if Self.EnergyEnabled() then
    Exit(True);

  if Self.GetLevel(ERSMinimapOrb.ENERGY) <= 1 then
    Exit;

  Mouse.Click(Self.Orbs[ERSMinimapOrb.ENERGY].Circle, EMouseButton.LEFT, True);
  Result := SleepUntil(Self.EnergyEnabled(), 200, 2*TICK);
end;


function TRSMinimap.DisableEnergy(): Boolean;
begin
  if not Self.EnergyEnabled() then
    Exit(True);

  if Self.GetLevel(ERSMinimapOrb.ENERGY) <= 1 then
    Exit(True);

  Mouse.Click(Self.Orbs[ERSMinimapOrb.ENERGY].Circle, EMouseButton.LEFT, True);
  Result := SleepUntil(not Self.EnergyEnabled(), 200, 2*TICK);
end;




function TRSMinimap.EnableSpecial(level: Integer): Boolean;
begin
  if Self.SpecialEnabled() then
    Exit(True);

  if Self.HasSpecial() and (Self.GetLevel(ERSMinimapOrb.SPECIAL) >= level) then
  begin
    Mouse.Click(Self.Orbs[ERSMinimapOrb.SPECIAL].Circle, EMouseButton.LEFT, True);
    Result := SleepUntil(Self.SpecialEnabled(), 200, 2*TICK);
  end;
end;


function TRSMinimap.DisableSpecial(): Boolean;
begin
  if not Self.SpecialEnabled() or Self.HasSpecial() then
    Exit(True);

  Mouse.Click(Self.Orbs[ERSMinimapOrb.SPECIAL].Circle, EMouseButton.LEFT, True);
  Result := SleepUntil(not Self.SpecialEnabled(), 200, 2*TICK);
end;




function TRSMinimap.FindFlag(out pt: TPoint): Boolean;
begin
  pt := Target.FindImage(Self.FLAG, 0, Self.Bounds);
  Result := Self.Polygon.Contains(pt);
end;


function TRSMinimap.HasFlag(): Boolean;
var
  p: TPoint;
begin
  Result := Self.FindFlag(p);
end;


function TRSMinimap.WaitFlag(time: Integer = 600; interval: Integer = -1): Boolean;
begin
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(Self.HasFlag(), interval, time);
end;



function TRSMinimap.CountColor(color: TColor; tolerance: Single = 0): Integer;
var
  tpa: TPointArray;
begin
  tpa := Target.FindColor(color, tolerance, Self.Bounds);
  Result := tpa.ExtractPolygon(Self.Polygon).Length;
end;

function TRSMinimap.CountColor(color: TColorTolerance): Integer; overload;
var
  tpa: TPointArray;
begin
  tpa := Target.FindColor(color, Self.Bounds);
  Result := tpa.ExtractPolygon(Self.Polygon).Length;
end;



function TRSMinimap.ColorPercent(color: TColor; tolerance: Single = 0): Single;
var
  count: Integer;
begin
  count := Self.CountColor(color, tolerance);
  Result := count / Self.Polygon.Area;
end;

function TRSMinimap.ColorPercent(color: TColorTolerance): Single; overload;
var
  count: Integer;
begin
  count := Self.CountColor(color);
  Result := count / Self.Polygon.Area;
end;



function TRSMinimap.Normalize(pt: TPoint; angle: Single): TPoint;
begin
  with pt.Rotate(-angle, Self.Center) do
  begin
    Result.X := Round((X - Self.Center.X) / 4) * 4 + Self.Center.X;
    Result.Y := Round((Y - Self.Center.Y) / 4) * 4 + Self.Center.Y;
  end;
end;

function TRSMinimap.NormalizeEx(vector: TVector2; angle: Single): TVector2;
begin
  with vector.Rotate(-angle, Self.Center.X, Self.Center.Y) do
  begin
    Result.X := Round((X - Self.Center.X) / 4) * 4 + Self.Center.X;
    Result.Y := Round((Y - Self.Center.Y) / 4) * 4 + Self.Center.Y;
  end;
end;



function TRSMinimap._GetDots(img: TImage): TPointArray;
var
  dot: ERSMinimapDot;
  tmp: TTarget;
begin
  tmp := new TTarget();
  tmp.SetImage(img.Copy());
  for dot in [ERSMinimapDot.PLAYER, ERSMinimapDot.NPC, ERSMinimapDot.ITEM] do
    Result += tmp.FindImageEx(Self.DotIMGs[dot], 0, -1);
end;

function TRSMinimap.GetDots(dot: ERSMinimapDot; bounds: TBox): TPointArray;
const
  COLORS: array [ERSMinimapDot] of array [0..1] of TColor = [
    [$FEFEFE, $ECECEC], [$02FCFC, $00EDED], [$0606FC, $0000F1]
  ];
var
  img: TImage;
  y,x: Integer;
begin
  bounds := bounds.Clip(Self.Polygon.Bounds());
  img := Target.GetImage(bounds);

  for y := 0 to img.Height-2 do
    for x := 0 to img.Width-2 do
      if (img.Pixel[x,y] = COLORS[dot][0]) and (img.Pixel[x+1,y] = COLORS[dot][1]) then
        Result += [x + bounds.X1+2, y + bounds.Y1-1];
end;

function TRSMinimap.GetDots(dot: ERSMinimapDot): TPointArray; overload;
begin
  Result := Self.GetDots(dot, Self.Polygon.Bounds());
end;

function TRSMinimap.GetDots(dots: ERSMinimapDots; bounds: TBox): TRSMinimapDotArray; overload;
var
  dot: ERSMinimapDot;
  b: TBox;
begin
  b := bounds.Clip(Self.Polygon.Bounds());
  for dot in dots do
    Result[dot] := Target.FindImageEx(Self.DotIMGs[dot], 0, -1, b);
end;

function TRSMinimap.GetDots(dots: ERSMinimapDots = [ERSMinimapDot.PLAYER, ERSMinimapDot.NPC, ERSMinimapDot.ITEM]): TRSMinimapDotArray; overload;
begin
  Result := Self.GetDots(dots, Self.Polygon.Bounds());
end;



function TRSMinimap.CleanImage(img: TImage; radius: Integer): TImage;

  function _DotTPA(pt: TPoint; bounds: TBox): TPointArray;
  var
    q: TPoint;
  begin
    if bounds.Contains(pt) then
      Exit([
        pt.Offset(0,-1), pt.Offset(1,-1),
        pt.Offset(-1,0), pt, pt.Offset(1,0), pt.Offset(2,0),
        pt.Offset(-1,1), pt.Offset(0,1), pt.Offset(1,1), pt.Offset(2,1),
        pt.Offset(0,2), pt.Offset(1,2),
        pt.Offset(-1,2), pt.Offset(2,2), pt.Offset(0,3), pt.Offset(1,3)
      ]);

    q := pt.Offset(0,-1);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(1,-1);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(-1,0);
    if bounds.Contains(q) then Result += q;

    Result += pt;

    q := pt.Offset(1,0);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(2,0);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(-1,1);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(0,1);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(1,1);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(2,1);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(0,2);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(1,2);
    if bounds.Contains(q) then Result += q;

    q := pt.Offset(-1,2);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(2,2);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(0,3);
    if bounds.Contains(q) then Result += q;
    q := pt.Offset(1,3);
    if bounds.Contains(q) then Result += q;
  end;

var
  tpa: TPointArray;
  pt: TPoint;
  bounds: TBox;
begin
  bounds := [1,1,img.Width-2, img.Height-2];
  for pt in Self._GetDots(img) do
    tpa += _DotTPA(pt, bounds);

  with img.Center do
  begin
    tpa += [X,Y];
    tpa += [X-1,Y];
    tpa += [X+1,Y];
    tpa += [X,Y+1];
    tpa += [X+1,Y+1];
    tpa += [X-1,Y+1];
    tpa += [X,Y-1];
    tpa += [X-1,Y-1];
    tpa += [X+1,Y-1];
  end;

  tpa += img.FindColor($0000FF, 0); //flag
  tpa += img.FindColor($0456C6, 0); //idk ask slacky/olly
  tpa += img.FindColor($11335D, 0); //idk ask slacky/olly

  tpa += img.FindColor($010000, 0); //replaced black
  tpa := tpa.SortFrom(img.Center);

  Result := img.Blend(tpa, 6);
end;


function TRSMinimap.GetCleanImage(angle: Single = $FFFF; radius: Integer = -1): TImage;
var
  img, clean: TImage;
  center: TPoint;
begin
  if angle = $FFFF then angle := Self.CompassRadians;

  if radius = -1 then
    radius := Self.Radius;

  img := Target.GetImage(Self.Bounds);
  img.ReplaceColor($0, $1, 0.23);

  clean := Self.CleanImage(img, radius);

  center := clean.Center;
  if RSClient.Mode = ERSMode.FIXED then
    center := center.Offset(0, -8)
  else
    center := center.Offset(-2, -1);

  clean.Crop(TBox.Create(center, radius, radius));

  Result := clean.Rotate(EImageRotateAlgo.BILINEAR, angle, False);
end;



function TRSMinimap.ScaleMinimap(img: TImage; scaling: Integer; radius: Integer = -1): TImage;
var
  tpa: TPointArray;
begin
  Result := img.Copy();

  if radius = -1 then
    radius := Self.Radius;

  Result.Crop(TBox.Create(Result.Center, radius, radius));
  tpa := Result.FindColor($0, 0).SortFrom(Result.Center);

  Result := Result.Blend(tpa, 8);
  Result := Result.Downsample(scaling);

  Result.DrawColor := $0;
  Result.DrawCircleInverted(Result.Center, radius div scaling);
end;



function TRSMinimap.IsPlayerMoving(minShift: Integer = 500): Boolean;
begin
  Result := Target.GetPixelDifference(300, TBox.Create(Self.Center, 40, 40)).Length >= minShift;
end;


procedure TRSMinimap.WaitMoving(time: Integer = 20000; minShift: Integer = 500);
var
  timeout, countdown: TCountDown;
  moving: Boolean;
begin
  timeout.Start(time);
  countdown.Start(500);

  repeat
    moving := Self.IsPlayerMoving(minShift);
    if moving then countdown.Restart(0, 1000);
  until timeout.IsFinished or (not moving and countdown.IsFinished) or not Self.HasFlag();
end;

var

  Minimap: TRSMinimap;
