

{$DEFINE WL_SLOT_INTERFACE_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type

  TRSSlotInterface = record
    Name: String;
    Slots: TBoxArray;
    SlotsFunction: function (): TBoxArray of object;
  end;


procedure TRSSlotInterface.Setup(name: String; slots: TBoxArray; slotsFunction: function (): TBoxArray of object = nil);
begin
  Self.Name := name;
  Self.Slots := slots;
  Self.SlotsFunction := @slotsFunction;
end;


function TRSSlotInterface.Boxes(): TBoxArray;
begin
  if @Self.SlotsFunction <> nil then
    Result := Self.SlotsFunction()
  else
    Result := Self.Slots;

  if Result = [] then
    raise GetDebugLn(Self.Name, 'Slots and SlotsFunction are not set!');
end;

function TRSSlotInterface.Boxes(slots: TIntegerArray): TBoxArray; overload;
var
  tmp: TBoxArray;
  i: Integer;
begin
  tmp := Self.Boxes();
  for i := 0 to High(slots) do
    Result += tmp[slots[i]];
end;


function TRSSlotInterface.Box(slot: Integer): TBox;
begin
  Result := Self.Boxes()[slot];
end;



function TRSSlotInterface.Near(slot: Integer; slots: TIntegerArray; maxDist: Single = 60; removeSlot: Boolean = True): TIntegerArray;
var
  slt: Integer;
  boxes: TBoxArray;
  center: TPoint;
begin
  if removeSlot then
    slots.Remove(slot);

  if Length(slots) = 0 then
    raise GetDebugLn(Self.Name, 'RandomSlotNearby: No slots to choose from');

  boxes := Self.Boxes();
  center := boxes[slot].Center;

  for slt in slots do
    if center.InRange(boxes[slt].Center, maxDist) then
      Result += slt;
end;


function TRSSlotInterface.RandomNear(slot: Integer; slots: TIntegerArray; maxDist: Single = 60; removeSlot: Boolean = True; distribution: ERandomDistribution = ERandomDistribution.GAUSS): Integer;
var
  near, weights: TIntegerArray;
  slt: Integer;
  boxes: TBoxArray;
  center: TPoint;
begin
  near := Self.Near(slot, slots, maxDist, removeSlot);

  if distribution = ERandomDistribution.RANDOM then
    Exit(near.Random());

  boxes := Self.Boxes();
  center := boxes[slot].Center;

  for slt in near do
    weights += Round(center.DistanceTo(boxes[slt].Center));

  Result := near.Sorted(weights, True)[RandomLeft(0, High(near))];
end;



function TRSSlotInterface.GetUnder(pt: TPoint): Integer;
var
  i: Integer;
  slots: TBoxArray;
begin
  slots := Self.Boxes();
  for i := 0 to High(slots) do
    if slots[i].Contains(pt) then
      Exit(i);
  Result := -1;
end;



function TRSSlotInterface.GetUsed(): TIntegerArray;
var
  slots: TBoxArray;
  i: Integer;
begin
  slots := Self.Boxes();
  for i := 0 to High(slots) do
    if Target.HasColor(TRSItem.Border, 8.5, 1, slots[i]) or
       Target.HasColor(TRSItem.BorderWhite, 0, 1, slots[i]) then
      Result += i;
end;


function TRSSlotInterface.GetEmpty(): TIntegerArray;
var
  slots: TBoxArray;
  i: Integer;
begin
  slots := Self.Boxes();
  for i := 0 to High(slots) do
    if not Target.HasColor(TRSItem.Border, 8.5, 1, slots[i]) and
       not Target.HasColor(TRSItem.BorderWhite, 0, 1, slots[i]) then
      Result += i;
end;



function TRSSlotInterface.IsUsed(slot: TBox): Boolean;
begin
  Result := TRSItem.InBounds(slot);
end;

function TRSSlotInterface.IsUsed(slot: Integer): Boolean; overload;
begin
  Result := TRSItem.InBounds(Self.Box(slot));
end;


function TRSSlotInterface.IsEmpty(slot: TBox): Boolean;
begin
  Result := not TRSItem.InBounds(slot);
end;

function TRSSlotInterface.IsEmpty(slot: Integer): Boolean; overload;
begin
  Result := not TRSItem.InBounds(Self.Box(slot));
end;



function TRSSlotInterface.IsFaded(slot: TBox): Boolean;
begin
  Result := not TRSItem.InBounds(slot, 0) and TRSItem.InBounds(slot);
end;

function TRSSlotInterface.IsFaded(slot: Integer): Boolean; overload;
var
  b: TBox;
begin
  b := Self.Box(slot);
  Result := not TRSItem.InBounds(b, 0) and TRSItem.InBounds(b);
end;



function TRSSlotInterface.WaitFade(slot: TBox; time: Integer = 200): Boolean;
begin
  if not Self.IsFaded(slot) then Exit;
  Result := SleepUntil(TRSItem.InBounds(slot, 0), 100, time);
end;

function TRSSlotInterface.WaitFade(slot: Integer; time: Integer = 200): Boolean; overload;
begin
  Result := Self.WaitFade(Self.Box(slot), time);
end;



function TRSSlotInterface.Count(): Integer;
begin
  Result := Length(Self.GetUsed());
end;


function TRSSlotInterface.CountEmpty(): Integer;
begin
  Result := Length(Self.GetEmpty());
end;



function TRSSlotInterface.WaitCount(count: Int32; time: Int32 = 600; interval: Int32 = -1): Boolean;
begin
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(Self.Count() = count, interval, time);
end;



function TRSSlotInterface.ReadStack(slot: TBox): Integer;
begin
  Result := TRSItem.ReadStack(slot);
end;

function TRSSlotInterface.ReadStack(slot: Integer): Integer; overload;
begin
  Result := TRSItem.ReadStack(Self.Box(slot));
end;



procedure TRSSlotInterface.Hover(slot: Integer);
begin
  Mouse.Move(Self.Box(slot));
end;



procedure TRSSlotInterface.Click(slot: Integer; button: EMouseButton = EMouseButton.LEFT);
begin
  Self.Hover(slot);
  Target.MouseClick(button);
end;



function TRSSlotInterface.Move(slot, destination: Integer): Boolean;
var
  slots: TBoxArray;
begin
  if slot = destination then Exit(True);

  slots := Self.Boxes([slot, destination]);

  if not Self.IsUsed(slots[0]) then Exit;

  Mouse.Move(slots[0]);
  Mouse.Hold(EMouseButton.LEFT);
  Sleep(50, 100, ERandomDir.LEFT);
  Mouse.Move(slots[1]);
  Mouse.Release(EMouseButton.LEFT);
  Sleep(50, 100, ERandomDir.Left);
  Result := Self.IsUsed(slots[1]);
end;



function TRSSlotInterface.Interact(slot: Integer; option: String = ''): Boolean;
begin
  Self.Hover(slot);

  if (option <> '') and not MainScreen.IsUpText(option) then
    Exit(ChooseOption.Select(option));

  Target.MouseClick(EMouseButton.LEFT);
  Result := True;
end;
