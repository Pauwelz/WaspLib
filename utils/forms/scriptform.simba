
{$DEFINE WL_SCRIPTFORM_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}

type

  TScriptForm = record
    Form: TLazForm;
    PageControl: TLazPageControl;
    Start: TLazButton;

    FormClosed: Boolean;
  end;



{$H-}

procedure TScriptForm.OnClose(sender: TLazObject; var closeAction: ELazFormCloseAction);
begin
  Self.FormClosed := True;
end;


procedure TScriptForm.OnStart(sender: TLazObject);
begin
  Self.Form.OnClose := nil;
  Self.Form.Close();
end;
{$H+}



procedure TScriptForm.Setup(caption: String = 'WaspLib');
var
  panel: TLazPanel;
begin
  ProfileForm.Setup();
  
  Self.Form := TLazForm.Create();

  Self.Form.Caption := caption + ' Settings';
  Self.Form.Width := 800;
  Self.Form.Height := 600;

  Self.Form.Constraints.MaxWidth := Self.Form.Width;
  Self.Form.Constraints.MinWidth := Self.Form.Width;
  Self.Form.Constraints.MaxHeight := Self.Form.Height;
  Self.Form.Constraints.MinHeight := Self.Form.Height;

  Self.Form.Position := ELazFormPosition.ScreenCenter;
  Self.Form.OnClose  := @Self.OnClose;

  Self.PageControl := TLazPageControl.Create(Self.Form);
  Self.PageControl.Parent := Self.Form;
  Self.PageControl.Align := ELazAlign.Client;
  Self.PageControl.Width := 800;
  Self.PageControl.Height := 570;

  panel := TLazPanel.Create(Self.Form);
  panel.Parent := Self.Form;
  panel.Align := ELazAlign.Bottom;
  panel.Height := 40;

  ProfileForm.SetupUI(panel);

  // Start button
  Self.Start := TLazButton.CreateEx(panel, 'Start', '', 0, 0, Floor(80 * FormUtils.Scale));
  Self.Start.Align := ELazAlign.Right;
  Self.Start.OnClick := @Self.OnStart;
  Self.Start.BorderSpacing.Top := 3;
  Self.Start.BorderSpacing.Left := 3;
  Self.Start.BorderSpacing.Right := 6;
  Self.Start.BorderSpacing.Bottom := 3;
end;


function TScriptForm.CreateTab(caption: String): TLazTabSheet;
var
  w, h: Integer;
begin
  w := Round(Self.PageControl.Width / FormUtils.Scale);
  h := Round(Self.PageControl.Height / FormUtils.Scale);

  Result := Self.PageControl.AddTab();
  Result.Caption := caption;
  Result.Width := w;
  Result.Height := h;

  Self.PageControl.ActiveTabIndex := 0;
end;


procedure TScriptForm.AddTab(tab: TLazTabSheet);
begin
  tab.Parent := Self.PageControl;
  Self.PageControl.InsertComponent(tab);
end;



procedure TScriptForm.Run();
begin
  try
    Self.Form.ShowModal();
  except
    WriteLn(GetExceptionMessage());
  finally
    Self.Form.Free();
  end;

  if Self.FormClosed then
    TerminateScript('Thank you for using WaspScripts');
end;
