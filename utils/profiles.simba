
{$DEFINE WL_PROFILES_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}

var

  ProfileIndex: Integer;

type
  TProfile = record
    Name, Username, Password, Pin: String;
    Worlds: TIntegerArray;
    Active: Boolean;
  end;


  TProfileArray = array of TProfile;


procedure TProfileArray.Add(name, user, pass: String; pin: String = ''; worlds: TIntegerArray = []);
begin
  Self += [name, user, pass, pin, worlds, true];
end;

procedure TProfileArray.NextPlayer(disableCurrent: Boolean);

  function _Next(): Integer;
  var
    i: Integer;
  begin
    for i := ProfileIndex + 1 to High(Self) do
      if Self[i].Active then
        Exit(i);

    for i := 0 to ProfileIndex - 1 do // wrap around
      if Self[i].Active then
        Exit(i);

    Result := -1;
  end;

begin
  Self[ProfileIndex].Active := not disableCurrent;

  ProfileIndex := _Next();
  if ProfileIndex < 0 then
    raise GetDebugLn('Profiles', 'No active Profiles to switch to.');
end;


function TProfileArray.Get(): TProfile;
begin
  if Self = [] then
    raise GetDebugLn('Profiles', 'No profiles declared: Add a profile if you want scripts to handle login.');
  if not InRange(ProfileIndex, 0, High(Self)) then
    raise GetDebugLn('Profiles', 'Profile is out of range: Profile number selected in script does not exist.');

  Result := Self[ProfileIndex];
end;


function TProfileArray.GetPin(): String;
begin
  Result := Self.Get().Pin;
  if (Length(Result) <> 4) or (not Result.IsNumeric) then
    raise GetDebugLn('Profiles', 'Invalid bank pin.');
end;


procedure TProfileArray.Load();
var
  i: Integer;
  parser: TJSONParser;
  worldsJSON: TJSONItem;
  path, name, username, password, pin: String;
  worlds: TIntegerArray;
begin
  Self := [];
  path := WLEnv.ConfigsDir + 'profiles' + PATH_SEP;
  i := -1;

  // Load each account file in order
  while FileExists(path + ToStr(Inc(i)) + '.json') do
  begin
    parser := new TJSONParser();
    try
      parser.Load(path + ToStr(i) + '.json');
      if not parser.GetString('name', name) then raise 'name error';
      if not parser.GetString('username', username) then raise 'username error';
      if not parser.GetString('password', password) then raise 'password error';
      if not parser.GetString('pin', pin) then raise 'pin error';
      if not parser.GetArray('worlds', worldsJSON) then raise 'worlds error';
    except
      WriteLn GetDebugLn('Profiles', 'Error loading profile ' + ToStr(i) + ': ' + GetExceptionMessage(), ELogLevel.ERROR);
      FileDelete(path + ToStr(i) + '.json');
      Continue;
    end;

    //Convert world string to integer array
    for i := 0 to worldsJSON.Count-1 do
      worlds += worldsJSON.Item[i].AsInt;

    Self.Add(name, username, password, pin, worlds);
  end;
end;


function TProfileArray.Contains(name: String): Boolean;
var
  i: Integer;
begin
  name := name.Trim().ToLower();
  for i := 0 to High(Self) do
    if Self[i].Name.ToLower() = name then
      Exit(True);
end;

var

  Profiles: TProfileArray;
