
{$DEFINE WL_PROGRESS_REPORT_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}


type
  TProgressReport = record
    Headers: TStringArray;
    ValueProvider: function: TStringArray of Object;
    Title: String;
    PrintTimer: TCountDown;
  end;


procedure TProgressReport.Setup(title: String = 'ProgressReport'; headers: TStringArray = []; valueProvider: function: TStringArray of Object = nil; printInterval: Integer = ONE_MINUTE * 5);
begin
  Self.Headers := headers;
  Self.Title := title;
  Self.PrintTimer.Start(printInterval);
  Self.ValueProvider := @valueProvider;
end;


function TProgressReport.Generate(values: TStringArray): String;
var
  i, totalW, headersW, valuesW: Integer;
  lines, btm, header: String;
begin
  if Length(Self.Headers) <> Length(values) then
    raise 'TProgressReport.GenerateReport: Header count (' +
          IntToStr(Length(Self.Headers)) + ') must match value count (' +
          IntToStr(Length(values)) + ')';

  for i := 0 to High(Self.Headers) do
  begin
    headersW := Max(headersW, Length(Self.Headers[i]) + 2);
    valuesW := Max(valuesW, Length(values[i]));
  end;

  totalW := headersW + valuesW;

  for i := 0 to High(Self.Headers) do
    lines += '| ' + Self.Headers[i].PadRight(headersW) +
             ' ' + values[i].PadRight(valuesW) + ' |' + LINE_SEP;

  header := '+ ' + Self.Title + ' ' + '-' * (totalW - Length(Self.Title) + 1) + '+';
  btm := '+' + '-' * (totalW + 3) + '+';

  Result := header + LINE_SEP + lines + btm;
end;


procedure TProgressReport.Print();
begin
  if not Self.PrintTimer.IsFinished then
    Exit;

  WriteLn Self.Generate(Self.ValueProvider());
  Self.PrintTimer.Restart();
end;

{$H-}
var
  ProgressReport: TProgressReport;
{$H+}
